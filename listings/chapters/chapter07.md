# Making music on the BBC micro

## Chapter 7

### Simple sound effects
```
10 REM PROGRAM 7.1
20 REM Examples of Channel 0
30 REM Sound Effects
40
50 REM Machine Gun
60 FOR Burst=1 TO 3
70 FOR Bullet=1 TO 12
80 SOUND0,-15,5,1
90 SOUND0,-15,6,1
100 NEXT Bullet
110 PROCDelay(100)
120 NEXT Burst
130 PROCDelay(100)
140
150 REM Ricochet
160 ENVELOPE1,132,28,-1,0,1,28,0,126,-8,-3,-6,126,80
170 ENVELOPE2,6,0,0,0,0,0,0,126,-8,-3,-6,126,80
180 FOR Shot=1 TO 3
190 SOUND2,1,149,20
200 SOUND0,2,6,20
210 NEXT Shot
220 PROCDelay(400)
230
240 REM Cymbal or Anvil
250 ENVELOPE1,3,0,0,0,0,0,0,126,-4,-2,-4,126,100
260 ENVELOPE2,3,0,0,0,0,0,0,80,-2,-2,-2,80,40
270 FOR Clang=1 TO 8
280 SOUND&101,1,197,1
290 SOUND&100,2,4,4
300 PROCDelay(100)
310 NEXT Clang
320 PROCDelay(200)
330
340 REM Creature
350 ENVELOPE1,4,0,0,0,0,0,0,32,-1,0,-4,126,0
360 SOUND1,0,220,0
370 FOR Step=1 TO 8
380 FOR Splodge=5 TO 7
390 SOUND0,1,Splodge,Splodge-3 
400 NEXT Splodge
410 PROCDelay(200)
420 NEXT Step
430
440 REM Mad Factory
450 SOUND1,0,100,0
460 FOR Work=1 TO 16
470 FOR Noise=1 TO 7
480 SOUND0,-12,Noise,2
490 NEXT Noise
500 NEXT Work
510 PROCDelay(200)
520
530 REM Space Ship Taking Off
540 ENVELOPE1,40,0,0,0,0,0,0,126,0,0,-2,126,126
550 SOUND0,-15,7,150
560 FOR Engines=180 TO 255 STEP.5
570 SOUND1,0,Engines,1
580 NEXT Engines
590 SOUND0,1,7,100
600 END
610
620 DEF PROCDelay(Time)
630 TIME=0:REPEAT UNTIL TIME>Time
640 ENDPROC
```

#### Helicopter
```
80 SOUND0,-15,4,1
90 SOUND0,-15,5,2
```

#### Car wonâ€™t start
```
95 SOUND0,-15,6,1
```
### Exploring the sound channel

Add this to listing 6.1 (envelope comparison)
```
185 SOUND0,-15,3,40
```
#### Combined with program 6.1
```
10 REM PROGRAM 6.1 with Noise
20 REM Envelope Comparisons 
30
40 VDU15
50 ON ERROR GOTO 230
60 REM Reset Delay on Keys
70 *FX12,0
80 *KEY0 RUN|M
90 *KEY10 OLD|MLIST|M
100 PROCEnvelope
110
120 REPEAT
130 PRINT"Which Envelope?";
140 REPEAT
150 Env=GET-48
160 UNTIL Env>-1 AND Env<10
170 PRINT;TAB(16)Env
180 IF Env=0 SOUND1,-12,Pitch,40 ELSE SOUND1,Env,Pitch,40
185 SOUND0,-15,3,40
190 UNTIL FALSE
200
210 REM Escape Routine
220 REM Speed Up Key Repeat
230 *FX12,6
240 *FX11,20
250 ON ERROR WHEN 280,
260 ERROR
270
280 DEF PROCEnvelope
290 Pitch=149
300 ENVELOPE1,4,0,0,1,1,0,1,4,-1,0,-3,126,80
310 ENVELOPE2,2,0,0,1,2,0,2,4,-1,0,-3,126,80
320 ENVELOPE3,3,0,0,1,2,0,2,4,-1,0,-3,126,80
330 ENVELOPE4,4,-2,1,1,1,1,1,4,-1,0,-3,126,80
340 ENVELOPE5,6,1,-2,1,1,1,1,4,-1,0,-3,126,80
350 ENVELOPE6,4,1,1,1,1,1,1,4,-1,0,-3,126,80
360 ENVELOPE7,4,0,8,-8,0,1,1,16,-1,0,-3,126,80
370 ENVELOPE8,4,0,28,-28,0,1,1,16,-1,0,-3,126,80
380 ENVELOPE9,8,0,8,-12,1,1,1,16,-1,0,-3,126,80
390 ENDPROC
```

### Sound Effects Generator program
```
10 REM PROGRAM 7.2
20 REM Sound Effects Generator
30
40 *TV255,1
50 MODE7
60
70 PROCSetUp
80 PROCOptions
90
100 REPEAT
110 PROCInput
120 REM Cursor Off
130 VDU23;11,0;0;0;0
140 PROCAction
150 PROCPrintEnv
160 PROCOptions
170 REM Cursor On
180 VDU23;11,255;0;0;0
190 UNTIL Input$="Q"
200 END
210
220 DEF PROCSetUp
230 SN$="Both "
240 Rep$="On "
250 P=3
260 Pitch=101
270 Inp$="EPRSNTAQ"
280 PROCSetSN
290 ENDPROC
300
310 DEF PROCSetSN
320 SAA=126:SAD=-1:SAS=0:SAR=-4
330 SALA=126:SALD=80
340 NAA=126:NAD=-1:NAS=0:NAR=-4
350 NALA=126:NALD=80
360 ENDPROC
370
380 DEF PROCOptions
390
400 FOR Title=0 TO 1
410 PRINTTAB(6,Title)CHR$134;CHR$141"SOUND EFFECTS GENERATOR"
420 NEXT Title
430
440 PRINTTAB(12,3)CHR$133"O P T I O N S"
450 PRINTTAB(0,5)CHR$129"E"CHR$130"nvelope"
460 PRINTTAB(0,6)CHR$129"P"CHR$130"lay"
470 PRINTTAB(0,7)CHR$129"R"CHR$130"epeat Pitch Envelope"TAB(25)CHR$131;Rep$
480 PRINTTAB(0,8)CHR$129"S"CHR$130"ound or Noise"TAB(25)CHR$131;SN$
490 PRINTTAB(0,9)CHR$129"N"CHR$130"oise Parameter"TAB(25)CHR$131;"P = ";P
500 PRINTTAB(0,10)CHR$129"T"CHR$130"Parameter - Alter "
510 PRINTTAB(0,11)CHR$129"A"CHR$130"lter Pitch"TAB(25)CHR$131;Pitch
520 PRINTTAB(0,12)CHR$129"Q"CHR$130"uit"
530 ENDPROC
540
550 DEF PROCInput
560 PRINTTAB(26,13)CHR$131"?? ";
570 REPEAT
580 Input$=GET$
590 Action=INSTR(Inp$,Input$)
600 UNTIL Action>0
610 PRINTInput$
620 ENDPROC
630
640 DEF PROCAction
650 ON Action GOTO 660,670,680,690,700,710,720,730
660 PROCEnvelope:ENDPROC
670 PROCPlay:ENDPROC
680 PROCRepeat:ENDPROC
690 PROCSoundNoise:ENDPROC
700 PROCNoiseParam:ENDPROC
710 PROCTParam:ENDPROC
720 PROCAlterPitch:ENDPROC
730 ENDPROC
740
750 DEF PROCEnvelope
760 T1=RND(20):T=T1
770 PI1=-129+RND(256)
780 PI2=-129+RND(256)
790 PI3=-129+RND(256)
800 PN1=RND(256)-1
810 PN2=RND(256)-1
820 PN3=RND(256)-1
830 PROCPlay
840 ENDPROC
850
860 DEF PROCPlay
870 ENVELOPE1,T,PI1,PI2,PI3,PN1,PN2,PN3,SAA,SAD,SAS,SAR,SALA,SALD
880 ENVELOPE2,T,0,0,0,0,0,0,NAA,NAD,NAS,NAR,NALA,NALD
890 SOUND&11,1,Pitch,254
900 SOUND&10,2,P,254
910 ENDPROC
920
930 DEF PROCRepeat
940 IF Rep$="Off" Rep$="On ":T=T1:ENDPROC
950 IF Rep$="On " Rep$="Off":T=T1+128
960 ENDPROC
970
980 DEF PROCSoundNoise
990 PROCSetSN
1000 IF SN$="Sound" SN$="Noise":SAA=-126:SAD=0:SAS=0:SAR=-126:SALA=0:SALD=0:ENDPROC
1010 IF SN$="Noise" SN$="Both ":ENDPROC
1020 IF SN$="Both " SN$="Sound":NAA=-126:NAD=0:NAS=0:NAR=-126:NALA=0:NALD=0
1030 ENDPROC
1040
1050 DEF PROCNoiseParam
1060 IF P=3 P=7 ELSE IF P=7 P=3
1070 ENDPROC
1080
1090 DEF PROCTParam
1100 PRINTTAB(2,10)CHR$133;"Enter T Value 1 - 127"
1110 REPEAT
1120 PRINTTAB(24,10)CHR$133;CHR$136;
1130 INPUT T1
1140 PRINTTAB(26,10)SPC(8)
1150 UNTIL T1>0 AND T1<128
1160 IF Rep$="On ":T=T1 ELSE T=T1+128
1170 ENDPROC
1180
1190 DEF PROCAlterPitch
1200 PRINTTAB(2,11)CHR$133;" Enter Pitch 0 - 255 "
1210 REPEAT
1220 PRINTTAB(24,11)CHR$133;CHR$136;
1230 INPUT Pitch
1240 PRINTTAB(26,11)SPC8
1250 UNTIL Pitch>-1 AND Pitch<256
1260 ENDPROC
1270
1280 DEF PROCPrintEnv
1290 PRINTTAB(0,15)CHR$134"Channel 1"
1300 PRINTTAB(0,17)SPC(17)
1310 PRINTTAB(0,16)"ENVELOPE1,";T1;",";PI1;",";PI2;",";PI3;",";PN1;",";PN2;",";PN3;",";SAA;",";SAD;",";SAS;",";SAR;",";SALA;",";SALD
1320 PRINTTAB(0,19)CHR$134"Channel 0"
1330 PRINTTAB(0,21)SPC(17)
1340 PRINTTAB(0,20)"ENVELOPE2,";T;",0,0,0,0,0,0,";NAA;",";NAD;",";NAS;",";NAR;",";NALA;",";NALD
1350 ENDPROC
```

#### Bugs

Should 1310 use T and not T1?
```
1310 PRINTTAB(0,16)"ENVELOPE1,";T;",";PI1;",";PI2;",";PI3;",";PN1;",";PN2;",";PN3;",";SAA;",";SAD;",";SAS;",";SAR;",";SALA;",";SALD
```
In fact, some of the variables are not declared (T, PI1, PI2, PI3, PN1, PN2, PN3)), if you press "S" as the first key, you get a "No such variable" error.

This line works:
```
1310 PRINTTAB(0,16)"ENVELOPE1,";SAA;",";SAD;",";SAS;",";SAR;",";SALA;",";SALD
```
Then there is a similar error in 1340
```
1340 PRINTTAB(0,20)"ENVELOPE2,";T;",0,0,0,0,0,0,";NAA;",";NAD;",";NAS;",";NAR;",";NALA;",";NALD
```
This line works:
```
1340 PRINTTAB(0,20)"ENVELOPE2,";NAA;",";NAD;",";NAS;",";NAR;",";NALA;",";NALD
```
This is because `PROCEnvelope` is not called (which is where the undefined variables (T, PI1, PI2, PI3, PN1, PN2, PN3) are set, prior to `PROCPrintEnv`), if "S" is the first key press causing `PROCSoundNoise` to be called, via `PROCAction`, in the main loop. However, `PROCEnvelope` calls the `PROCPlay` start after setting.

A fix would be to separate `PROCEnvelope` into two calls `PROCSetEnvelope` and `PROCPlay`, where `PROCSetEnvelope` sets the undefined variables. Then `PROCSetEnvelope` can also be called as part of `PROCSetUp`, either just after or just before `PROCSetSN`.

Line changes:

- 285
- 750-825

Like this:

```
10 REM PROGRAM 7.2.1 (fixed)
20 REM Sound Effects Generator
30
40 *TV255,1
50 MODE7
60
70 PROCSetUp
80 PROCOptions
90
100 REPEAT
110 PROCInput
120 REM Cursor Off
130 VDU23;11,0;0;0;0
140 PROCAction
150 PROCPrintEnv
160 PROCOptions
170 REM Cursor On
180 VDU23;11,255;0;0;0
190 UNTIL Input$="Q"
200 END
210
220 DEF PROCSetUp
230 SN$="Both "
240 Rep$="On "
250 P=3
260 Pitch=101
270 Inp$="EPRSNTAQ"
280 PROCSetSN
285 PROCSetEnvelope
290 ENDPROC
300
310 DEF PROCSetSN
320 SAA=126:SAD=-1:SAS=0:SAR=-4
330 SALA=126:SALD=80
340 NAA=126:NAD=-1:NAS=0:NAR=-4
350 NALA=126:NALD=80
360 ENDPROC
370
380 DEF PROCOptions
390
400 FOR Title=0 TO 1
410 PRINTTAB(6,Title)CHR$134;CHR$141"SOUND EFFECTS GENERATOR"
420 NEXT Title
430
440 PRINTTAB(12,3)CHR$133"O P T I O N S"
450 PRINTTAB(0,5)CHR$129"E"CHR$130"nvelope"
460 PRINTTAB(0,6)CHR$129"P"CHR$130"lay"
470 PRINTTAB(0,7)CHR$129"R"CHR$130"epeat Pitch Envelope"TAB(25)CHR$131;Rep$
480 PRINTTAB(0,8)CHR$129"S"CHR$130"ound or Noise"TAB(25)CHR$131;SN$
490 PRINTTAB(0,9)CHR$129"N"CHR$130"oise Parameter"TAB(25)CHR$131;"P = ";P
500 PRINTTAB(0,10)CHR$129"T"CHR$130"Parameter - Alter "
510 PRINTTAB(0,11)CHR$129"A"CHR$130"lter Pitch"TAB(25)CHR$131;Pitch
520 PRINTTAB(0,12)CHR$129"Q"CHR$130"uit"
530 ENDPROC
540
550 DEF PROCInput
560 PRINTTAB(26,13)CHR$131"?? ";
570 REPEAT
580 Input$=GET$
590 Action=INSTR(Inp$,Input$)
600 UNTIL Action>0
610 PRINTInput$
620 ENDPROC
630
640 DEF PROCAction
650 ON Action GOTO 660,670,680,690,700,710,720,730
660 PROCEnvelope:ENDPROC
670 PROCPlay:ENDPROC
680 PROCRepeat:ENDPROC
690 PROCSoundNoise:ENDPROC
700 PROCNoiseParam:ENDPROC
710 PROCTParam:ENDPROC
720 PROCAlterPitch:ENDPROC
730 ENDPROC
740
750 DEF PROCSetEnvelope
760 T1=RND(20):T=T1
770 PI1=-129+RND(256)
780 PI2=-129+RND(256)
790 PI3=-129+RND(256)
795 PN1=RND(256)-1
800 PN2=RND(256)-1
805 PN3=RND(256)-1
810 ENDPROC
815
820 DEF PROCEnvelope
825 PROCSetEnvelope
830 PROCPlay
840 ENDPROC
850
860 DEF PROCPlay
870 ENVELOPE1,T,PI1,PI2,PI3,PN1,PN2,PN3,SAA,SAD,SAS,SAR,SALA,SALD
880 ENVELOPE2,T,0,0,0,0,0,0,NAA,NAD,NAS,NAR,NALA,NALD
890 SOUND&11,1,Pitch,254
900 SOUND&10,2,P,254
910 ENDPROC
920
930 DEF PROCRepeat
940 IF Rep$="Off" Rep$="On ":T=T1:ENDPROC
950 IF Rep$="On " Rep$="Off":T=T1+128
960 ENDPROC
970
980 DEF PROCSoundNoise
990 PROCSetSN
1000 IF SN$="Sound" SN$="Noise":SAA=-126:SAD=0:SAS=0:SAR=-126:SALA=0:SALD=0:ENDPROC
1010 IF SN$="Noise" SN$="Both ":ENDPROC
1020 IF SN$="Both " SN$="Sound":NAA=-126:NAD=0:NAS=0:NAR=-126:NALA=0:NALD=0
1030 ENDPROC
1040
1050 DEF PROCNoiseParam
1060 IF P=3 P=7 ELSE IF P=7 P=3
1070 ENDPROC
1080
1090 DEF PROCTParam
1100 PRINTTAB(2,10)CHR$133;"Enter T Value 1 - 127"
1110 REPEAT
1120 PRINTTAB(24,10)CHR$133;CHR$136;
1130 INPUT T1
1140 PRINTTAB(26,10)SPC(8)
1150 UNTIL T1>0 AND T1<128
1160 IF Rep$="On ":T=T1 ELSE T=T1+128
1170 ENDPROC
1180
1190 DEF PROCAlterPitch
1200 PRINTTAB(2,11)CHR$133;" Enter Pitch 0 - 255 "
1210 REPEAT
1220 PRINTTAB(24,11)CHR$133;CHR$136;
1230 INPUT Pitch
1240 PRINTTAB(26,11)SPC8
1250 UNTIL Pitch>-1 AND Pitch<256
1260 ENDPROC
1270
1280 DEF PROCPrintEnv
1290 PRINTTAB(0,15)CHR$134"Channel 1"
1300 PRINTTAB(0,17)SPC(17)
1310 PRINTTAB(0,16)"ENVELOPE1,";T1;",";PI1;",";PI2;",";PI3;",";PN1;",";PN2;",";PN3;",";SAA;",";SAD;",";SAS;",";SAR;",";SALA;",";SALD
1320 PRINTTAB(0,19)CHR$134"Channel 0"
1330 PRINTTAB(0,21)SPC(17)
1340 PRINTTAB(0,20)"ENVELOPE2,";T;",0,0,0,0,0,0,";NAA;",";NAD;",";NAS;",";NAR;",";NALA;",";NALD
1350 ENDPROC
```

### Using channel 0 to produce otherwise unobtainable low notes
```
10 FOR Pitch=200 TO 1 STEP-4
20 SOUND1 ,0,Pitch,10
30 SOUND0,- 15,3,10
40 NEXT Pitch
```

### Designing a rhythm unit

```
10 REM PROGRAM 7.3
20 REM Rhythm Unit
30
40 *TV255,1
50 MODE7
70 PROCSetUp
80 PROCTitlePage
90 PROCPlay
100 END
110
120 DEF PROCSetUp
130 Tempo=2
140 Rhythm=360
150 ENVELOPE1,1,0,0,0,0,0,0,126,-3,-3,-6,126,100
160 ENVELOPE2,1,0,0,0,0,0,0,32,-4,-4,-8,110,60
170
180 DEF PROCTitlePage
190 FOR Title=1 TO 2
200 PRINTTAB(6,Title)CHR$141;CHR$134"RHYTHM UNIT"
210 NEXT Title
220 PRINTTAB(1,4)CHR$129;"1";CHR$130;"Bossa Nova"
230 PRINTTAB(1,5)CHR$129;"2";CHR$130;"Rock"
240 PRINTTAB(1,6)CHR$129;"3";CHR$130;"12/8 Rock"
250 PRINTTAB(1,7)CHR$129;"4";CHR$130;"Swing"
260 PRINTTAB(1,8)CHR$129;"5";CHR$130;"Waltz"
270 PRINTTAB(1,9)CHR$129;"Q";CHR$130;"Quit"
280 ENDPROC
290
300 DEF PROCPlay
310 REPEAT
320 *FX15,1
330 IF INKEY(-49) Rhythm=450
340 IF INKEY(-50) Rhythm=480
350 IF INKEY(-18) Rhythm=520
360 IF INKEY(-19) Rhythm=550
370 IF INKEY(-20) Rhythm=580
380 READ Env,Pit,Dur
390 SOUND0,Env,Pit,Dur*Tempo
400 IF Env=0 RESTORE Rhythm
410 UNTIL INKEY(-17)
420 ENDPROC
430
440 REM Bossa Nova
450 DATA 1,4,2,2,5,2,2,5,2,1,4,2,2,5,2,2,5,2,1,4,2,2,5,2,0,0,0
460
470 REM Rock
480 DATA 1,5,2,2,5,2,1,4,2,1,4,1,1,5,2,2,5,2,2,5,1,1,4,1,2,4,1,2,4,2
490 DATA 1,5,2,2,5,2,1,4,2,1,4,1,1,5,2,2,5,2,2,5,1,1,4,1,2,5,1,1,6,1,1,6,1,0,0 ,0
500
510 REM 12/8 Rock
520 DATA 1,5,2,2,4,1,2,4,1,2,4,2,1,4,2,2,4,2,2,4,2,1,5,2,2,4,1,2,4,1,2,4,2,1,4,2,2,4,2,1,6,2,0,0,0
530
540 REM Swing
550 DATA 1,5,4,2,4,3,2,4,1,1,5,4,2,4,3,2,4,1,1,5,4,2,4,3,2,4,1,1,6,2,2,4,2,2,4,1,2,4,1,2,4,2,0,0,0
560
570 REM Waltz
580 DATA 1,5,4,2,4,4,2,4,4,1,6,4,2,4,4,2,4,4,1,5,4,2,4,4,2,4,4,1,6,4,2,4,2,2,4,2,2,5,2,1,5,2,0,0,0
```

### The CAPS LOCK and SHIFT LOCK lights and the ADVAL function

Replace 380, 390, 400 with
```
380 IF ADVAL(-5)>0 READ Env,Pit,Dur: SOUND0,Env,Pit,Dur*Tempo:IF Env=0 RESTORE Rhythm
```
Inserting the line:
```
10 REM PROGRAM 7.3.1 (fixed)
20 REM Rhythm Unit
30
40 *TV255,1
50 MODE7
70 PROCSetUp
80 PROCTitlePage
90 PROCPlay
100 END
110
120 DEF PROCSetUp
130 Tempo=2
140 Rhythm=360
150 ENVELOPE1,1,0,0,0,0,0,0,126,-3,-3,-6,126,100
160 ENVELOPE2,1,0,0,0,0,0,0,32,-4,-4,-8,110,60
170
180 DEF PROCTitlePage
190 FOR Title=1 TO 2
200 PRINTTAB(6,Title)CHR$141;CHR$134"RHYTHM UNIT"
210 NEXT Title
220 PRINTTAB(1,4)CHR$129;"1";CHR$130;"Bossa Nova"
230 PRINTTAB(1,5)CHR$129;"2";CHR$130;"Rock"
240 PRINTTAB(1,6)CHR$129;"3";CHR$130;"12/8 Rock"
250 PRINTTAB(1,7)CHR$129;"4";CHR$130;"Swing"
260 PRINTTAB(1,8)CHR$129;"5";CHR$130;"Waltz"
270 PRINTTAB(1,9)CHR$129;"Q";CHR$130;"Quit"
280 ENDPROC
290
300 DEF PROCPlay
310 REPEAT
320 *FX15,1
330 IF INKEY(-49) Rhythm=450
340 IF INKEY(-50) Rhythm=480
350 IF INKEY(-18) Rhythm=520
360 IF INKEY(-19) Rhythm=550
370 IF INKEY(-20) Rhythm=580
380 IF ADVAL(-5)>0 READ Env,Pit,Dur: SOUND0,Env,Pit,Dur*Tempo:IF Env=0 RESTORE Rhythm
410 UNTIL INKEY(-17)
420 ENDPROC
430
440 REM Bossa Nova
450 DATA 1,4,2,2,5,2,2,5,2,1,4,2,2,5,2,2,5,2,1,4,2,2,5,2,0,0,0
460
470 REM Rock
480 DATA 1,5,2,2,5,2,1,4,2,1,4,1,1,5,2,2,5,2,2,5,1,1,4,1,2,4,1,2,4,2
490 DATA 1,5,2,2,5,2,1,4,2,1,4,1,1,5,2,2,5,2,2,5,1,1,4,1,2,5,1,1,6,1,1,6,1,0,0 ,0
500
510 REM 12/8 Rock
520 DATA 1,5,2,2,4,1,2,4,1,2,4,2,1,4,2,2,4,2,2,4,2,1,5,2,2,4,1,2,4,1,2,4,2,1,4,2,2,4,2,1,6,2,0,0,0
530
540 REM Swing
550 DATA 1,5,4,2,4,3,2,4,1,1,5,4,2,4,3,2,4,1,1,5,4,2,4,3,2,4,1,1,6,2,2,4,2,2,4,1,2,4,1,2,4,2,0,0,0
560
570 REM Waltz
580 DATA 1,5,4,2,4,4,2,4,4,1,6,4,2,4,4,2,4,4,1,5,4,2,4,4,2,4,4,1,6,4,2,4,2,2,4,2,2,5,2,1,5,2,0,0,0
```

### Using sound effects in utility programs
Making a beep (equivalent to CTRL-G)
```
VDU 7
```

```
1000 DEF PROCSound
1010 SOUND0,-12,4,2
1020 ENDPROC
```


### Soundscapes: a total sound effects program
```
10 REM PROGRAM 7.4
20 REM Sea, Surf & Seagulls
30
40 ENVELOPE1,130,28,-1,0,1,28,0,63,-8,-4,-6,126,80
50 ENVELOPE2,2,0,0,0,0,0,0,63,-1,0,-4,126,116
60 Foghorn=0
70
80 REPEAT
90 Foghorn=Foghorn+1
100 Pitch=RND(64)+190
110 IF ADVAL(-6)>0 SOUND1,0,Pitch,0
120
130 Pulse=RND(20)+15
140 Lull1=RND(10)+25
150 Lull2=RND(10)+25
160 Flow=RND(6)+8
170 Ebb1=RND(2)+3
180 Force=RND(26)+100
190
200 Wave=RND(3)+3
210 Ebb2=RND(5)^3+129
220
230 REM Envelope for Waves
240 ENVELOPE3,Pulse,0,1,0,Lull1,1,Lull2,Flow,-Ebb1,-1,-1,Force,90
250
260 IF ADVAL(-5)>3 SOUND0,3,Wave,Ebb2: SOUND&1000,0,0,Ebb2
270
280 REM Seagull
290 IF ADVAL(-7)>0:IF RND(80)=1 SOUND2,1,RND(76)+101,20
300
310 REM Foghorn
320 IF ADVAL(-8)>0:IF Foghorn>140 SOUND&113,2,0,80:SOUND&112,2,0,80:IF Foghorn >140 Foghorn=0
330 UNTIL FALSE
```
#### Add
```
315 PRINT Foghorn
```
#### Remove ADVAL
```
260 SOUND0,3,Wave,Ebb2:SOUND&1000,0,0,Ebb2
```
